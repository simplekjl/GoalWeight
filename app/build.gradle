plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'jacoco'
}

apply from: "$rootProject.projectDir/ktlint.gradle"
apply from: "$rootProject.projectDir/coverage-android.gradle"
apply plugin: 'kotlin-kapt'

android {
    compileSdk Configs.compilation

    defaultConfig {
        applicationId Configs.applicationId

        minSdkVersion Configs.minSdk
        targetSdkVersion Configs.target

        versionCode project.VERSION_CODE.toInteger()
        versionName project.VERSION_NAME

        multiDexEnabled true
        buildConfigField "String", "DB_NAME", Configs.db_name
        testInstrumentationRunner Configs.testRunner

        testInstrumentationRunnerArguments clearPackageData: 'true'
        vectorDrawables {
            useSupportLibrary true
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'


        }
        debug {
            debuggable true
            testCoverageEnabled true
            applicationIdSuffix ".dev"
            versionNameSuffix "_dev"
        }
        qa {
            initWith debug

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            applicationIdSuffix ".qa"
            versionNameSuffix "_qa"
            matchingFallbacks = ['debug']
        }
    }

    testOptions {
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
        unitTests.returnDefaultValues = true
        animationsDisabled true
        unitTests.includeAndroidResources = true
    }

    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11
    }
    buildFeatures {
        viewBinding true
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion Configs.composeVersion
    }
    packagingOptions {
        exclude 'META-INF/licenses/ASM'
        exclude "**/attach_hotspot_windows.dll"
        pickFirst 'META-INF/*'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude("META-INF/*.kotlin_module")
    }
    configurations {
        cleanedAnnotations
        all {
            resolutionStrategy {
                exclude group: "org.jetbrains.kotlinx", module: "kotlinx-coroutines-debug"
                eachDependency { details ->
                    if ('org.jacoco' == details.requested.group) {
                        details.useVersion Versions.jacoco
                    }
                    exclude group: 'com.google.guava', module: 'listenablefuture'
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    // common classes for testing
    implementation project(':data')
    implementation project(':domain')
    implementation project(':common:ui')
    testImplementation project(':testshared')

    // Kotlin
    implementation JetbrainsLibraries.kotlin
    implementation JetbrainsLibraries.coroutinesCore
    implementation JetbrainsLibraries.coroutinesAndroid

    // Koin
    implementation KoinLibraries.core
    implementation KoinLibraries.android
    implementation KoinLibraries.compose

    // Google
    implementation GoogleLibraries.material

    // Androidx
    implementation AndroidxLibraries.core
    implementation AndroidxLibraries.collection
    implementation AndroidxLibraries.dynamicAnimation
    implementation AndroidxLibraries.navigationUI
    implementation AndroidxLibraries.navigationFragment
    implementation AndroidxLibraries.activityKtx
    implementation AndroidxLibraries.fragmentKtx

    // AndroidX Lifecycle
    implementation LifecyleLibraries.viewModel
    implementation LifecyleLibraries.runtime
    implementation LifecyleLibraries.liveData
    implementation LifecyleLibraries.process

    //run-time live data
    implementation ComposeLibraries.runtimeLiveData
    implementation ComposeLibraries.ui
    // Tooling support (Previews, etc.)
    implementation ComposeLibraries.uiTooling
    // Foundation (Border, Background, Box, Image, Scroll, shapes, animations, etc.)
    implementation ComposeLibraries.foundation
    // Material Design
    implementation ComposeLibraries.material
    // Navigation
    implementation ComposeLibraries.navigation
    implementation ComposeLibraries.runtime
    // chart
    implementation Libraries.MPchart
    // Room
    implementation Room.runtime
    implementation Room.test
    kapt Room.compiler

    // Used for displaying Preview
    implementation ComposeLibraries.activity
    implementation Libraries.fixture
    androidTestImplementation Libraries.fixture
    testImplementation Libraries.fixture
    implementation 'androidx.navigation:navigation-testing:2.5.2'

    androidTestImplementation ComposeLibraries.uiTestJunit4
    testImplementation ComposeLibraries.uiTestJunit4
    debugImplementation ComposeLibraries.uiTest

    testImplementation Libraries.robolectric
    androidTestImplementation AndroidxTestLibraries.testRunner
    androidTestImplementation AndroidxTestLibraries.core
    androidTestImplementation AndroidxTestLibraries.archCoreTest

    // need this for android instrumentation tests run
    androidTestImplementation ComposeLibraries.activity

    testImplementation AndroidxTestLibraries.archCoreTest
    testImplementation AndroidxTestLibraries.core
    testImplementation AndroidxTestLibraries.testJunit
    testImplementation AndroidxTestLibraries.testRules
    testImplementation MockkLibraries.mockk
    testImplementation MockkLibraries.mockkAndroid
    testImplementation GoogleLibraries.truth
    androidTestImplementation GoogleLibraries.truth

    testImplementation Libraries.robolectric

    coreLibraryDesugaring Libraries.desugarJdkLibs

    debugApi "androidx.customview:customview:1.2.0-alpha01"
    debugApi "androidx.customview:customview-poolingcontainer:1.0.0"
    implementation "com.google.accompanist:accompanist-systemuicontroller:0.17.0"
    implementation "com.google.accompanist:accompanist-swiperefresh:0.26.3-beta"
    implementation "com.google.guava:listenablefuture:9999.0-empty-to-avoid-conflict-with-guava"
}